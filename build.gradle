buildscript {
    repositories {
        maven { url 'http://oss.jfrog.org/repo' }
        mavenCentral()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:0.9.1-SNAPSHOT'
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.1.0'
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
    }
}

apply plugin: 'ratpack-groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'mongo'

repositories {
    maven { url 'http://oss.jfrog.org/repo' }
    mavenCentral()
    maven { url 'http://repo.springsource.org/repo' } // for springloaded
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 7, 'days'
    resolutionStrategy.cacheChangingModulesFor 7, 'days'
}

idea {
    project {
        jdkName '1.7'
        languageLevel '1.7'
    }
}

eclipse {
    classpath {
        defaultOutputDir = file('build-eclipse')
    }
}

dependencies {
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:2.1'

    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'joda-time:joda-time:1.6.1'
    compile 'org.mongodb:mongo-java-driver:2.11.2'

    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded 'org.springsource.springloaded:springloaded-core:1.1.1'
}

test {
    exclude '**/*FunctionalSpec.*'
    exclude '**/*IntegrationSpec.*'
}

task startApp(type: StartRatpackTask) {
}

task integrationTest(dependsOn: 'startManagedMongoDb', type: Test) {
    include '**/*IntegrationSpec.*'
}

task functionalTest(dependsOn: 'startApp', type: Test) {
    include '**/*FunctionalSpec.*'
}

task console(dependsOn: 'classes', type: JavaExec) {
    main 'groovy.ui.Console'
    classpath sourceSets.main.runtimeClasspath
}