buildscript {
    repositories {
        maven { url 'http://oss.jfrog.org/repo' }
        mavenCentral()
    }
    dependencies {
        classpath('io.ratpack:ratpack-gradle:0.9.4') {
            exclude module: 'groovy-all'
        }
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
    }
}

apply plugin: 'ratpack-groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'codenarc'

repositories {
    maven { url 'http://oss.jfrog.org/repo' }
    mavenCentral()
    maven { url 'http://repo.springsource.org/repo' } // for springloaded
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main/groovy/core'
            srcDir 'src/main/groovy/infra'
            srcDir 'src/main/groovy/main'
        }
        resources {
            srcDir 'src/ratpack'
        }
    }
    test {
        groovy {
            srcDir 'src/test/groovy/core'
            srcDir 'src/test/groovy/infra'
            srcDir 'src/test/groovy/main'
        }
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 7, 'days'
    resolutionStrategy.cacheChangingModulesFor 7, 'days'
}

idea {
    project {
        jdkName '1.7'
        languageLevel '1.7'
    }
}

eclipse {
    classpath {
        defaultOutputDir = file('build-eclipse')
    }
}

codenarc {
    configFile = new File('config/codenarc/Codenarc.groovy')
    toolVersion = '0.21'
}

dependencies {
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6', {
        exclude module: 'groovy'
    }
    testCompile 'cglib:cglib-nodep:3.1'

    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'joda-time:joda-time:1.6.1'
    compile 'org.codehaus.gpars:gpars:1.1.0'
    compile 'org.codehaus.groovy:groovy-all:2.3.0'

    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded 'org.springsource.springloaded:springloaded-core:1.1.1'
}

test {
    exclude '**/*FunctionalSpec.*'
}

task functionalTest(type: Test) {
    include '**/*FunctionalSpec.*'
}
