buildscript {
    repositories {
        maven { url 'http://oss.jfrog.org/repo' }
        mavenCentral()
    }
    dependencies {
        classpath('io.ratpack:ratpack-gradle:0.9.8') {
            exclude module: 'groovy-all'
        }
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
    }
}

apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'codenarc'

repositories {
    maven { url 'http://oss.jfrog.org/repo' }
    mavenCentral()
    maven { url 'http://repo.springsource.org/repo' } // for springloaded
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy/core', 'src/main/groovy/infra', 'src/main/groovy/main']
        }
        resources {
            srcDir 'src/ratpack'
        }
    }
    test {
        groovy {
            srcDirs = ['src/test/groovy/core', 'src/test/groovy/infra', 'src/test/groovy/main']
        }
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 7, 'days'
    resolutionStrategy.cacheChangingModulesFor 7, 'days'
}

idea {
    project {
        jdkName '1.7'
        languageLevel '1.7'
    }
}

eclipse {
    classpath {
        defaultOutputDir = file('build-eclipse')
    }
}

codenarc {
    toolVersion = '0.21'
}

codenarcMain {
    configFile = new File('config/codenarc/CodenarcMain.groovy')
}

codenarcTest {
    configFile = new File('config/codenarc/CodenarcTest.groovy')
}

dependencies {
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7', {
        exclude module: 'groovy'
    }
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'com.natpryce:snodge:1.3.1.1'

    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.slf4j:slf4j-log4j12:1.7.7'
    compile 'joda-time:joda-time:1.6.1'
    compile 'org.codehaus.gpars:gpars:1.1.0'
    compile 'org.codehaus.groovy:groovy-all:2.3.2'
    compile ratpack.dependency('codahale-metrics')

    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded 'org.springsource.springloaded:springloaded-core:1.1.1'
}

test {
    exclude '**/*FunctionalSpec.*'
    maxParallelForks = Math.round(Runtime.runtime.availableProcessors() / 2)
}

task functionalTest(type: Test) {
    include '**/*FunctionalSpec.*'
    environment 'PERSISTED_JSON_FILE_PATH': temporaryDirPath()
}

run {
    environment 'PERSISTED_JSON_FILE_PATH': temporaryDirPath()
}

private String temporaryDirPath() {
    java.nio.file.Files.createTempDirectory(null, [] as java.nio.file.attribute.FileAttribute[]).toString()
}
